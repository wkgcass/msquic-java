import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    def PNI_VERSION = '21.0.0.16'
    ext.set("PNI_VERSION", PNI_VERSION)

    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'io.vproxy', name: 'pni-exec', version: PNI_VERSION
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

def PNI_API_VERSION = project.PNI_VERSION

group = 'io.vproxy'
version = '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

subprojects {
    apply plugin: 'java-library'

    group 'io.vproxy'
    version rootProject.version

    java {
        sourceCompatibility = '21'
        targetCompatibility = '21'
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.incremental = false
        options.compilerArgs += '--enable-preview'
    }
    tasks.withType(JavaExec) {
        jvmArgs += '--enable-preview'
        jvmArgs += '--enable-native-access=ALL-UNNAMED'
        javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
    }
    tasks.withType(Test) {
        jvmArgs += '--enable-preview'
        jvmArgs += '--enable-native-access=ALL-UNNAMED'

        testLogging {
            showStandardStreams = true
            events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.STARTED
            maxGranularity 100
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from "$buildDir/docs/javadoc"
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}

project(':template') {
    dependencies {
        implementation 'io.vproxy:pni-api-jdk21:' + PNI_API_VERSION
    }
    compileJava {
        options.compilerArgs += '-parameters'
    }
    task pniGenerate() {
        dependsOn compileJava

        def workingDir = project.rootProject.rootDir.getAbsolutePath()
        def gen = new io.vproxy.pni.exec.Generator(
            new io.vproxy.pni.exec.CompilerOptions()
                .setClasspath(List.of(workingDir + '/template/build/classes/java/main'))
                .setJavaOutputBaseDirectory(workingDir + '/core/src/main/generated')
                .setCOutputDirectory(workingDir + '/core/src/main/c-generated')
                .setCompilationFlag(io.vproxy.pni.exec.CompilationFlag.TYPE_NAME_PREFIX, "PNI")
                .setCompilationFlag(io.vproxy.pni.exec.CompilationFlag.RELEASE_PNI_H_FILE, null)
                .setCompilationFlag(io.vproxy.pni.exec.CompilationFlag.RELEASE_PNI_C_FILE, null)
                .setCompilationFlag(io.vproxy.pni.exec.CompilationFlag.RELEASE_JNI_H_MOCK_FILE, null)
        )

        doLast {
            gen.generate()
        }
    }
    task pniCompile(type: Exec) {
        workingDir project.rootProject.rootDir.getAbsolutePath() + '/core/src/main/c'
        commandLine './make-quic.sh'

        dependsOn pniGenerate
    }
}

project(':core') {
    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', 'src/main/generated', 'src/main/module-info']
            }
        }
    }
    dependencies {
        api 'io.vproxy:pni-api-jdk21:' + PNI_API_VERSION
        api 'io.vproxy:commons:1.1.3'
    }
}

project(':sample') {
    dependencies {
        implementation project(':core')
    }
    task runSampleClient(type: JavaExec) {
        standardInput = System.in

        classpath = sourceSets.main.runtimeClasspath
        mainClass = 'io.vproxy.msquic.sample.Client'
        jvmArgs += '-Djava.library.path=' +
                project.rootProject.rootDir.getAbsolutePath() + '/core/src/main/c' +
                File.pathSeparator +
                System.getenv("MSQUIC_LD")
    }
    task runSampleServer(type: JavaExec) {
        standardInput = System.in

        classpath = sourceSets.main.runtimeClasspath
        mainClass = 'io.vproxy.msquic.sample.Server'
        jvmArgs += '-Djava.library.path=' +
                project.rootProject.rootDir.getAbsolutePath() + '/core/src/main/c' +
                File.pathSeparator +
                System.getenv("MSQUIC_LD")

        dependsOn project(':template').tasks.pniCompile
        dependsOn compileJava
    }
}
